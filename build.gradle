plugins {
    id 'java'
    id 'idea'
    id "io.freefair.lombok" version "5.1.0"
    id 'com.avast.gradle.docker-compose' version "0.13.2"
}


repositories {
    jcenter()
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.1'

    testImplementation 'io.cucumber:cucumber-java:6.2.2'
    testImplementation 'io.cucumber:cucumber-junit:6.2.2'
    testImplementation 'io.rest-assured:rest-assured:4.3.1'
    testImplementation 'org.aeonbits.owner:owner:1.0.12'
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task testeApiCucumber() {
    dependsOn assemble, testClasses
    doFirst {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty',
                    '--plugin', 'html:build/reports/feature.html',
                    '--plugin', 'json:build/reports/feature.json',
                    '--glue', 'qarentena.bdd.testes.servico', 'src/test/resources/features',
            ]
        }
    }
}


task testeSanidadeApi(type: Test) {
    environment "CUCUMBER_FILTER_TAGS", "@sanidade"
}

task testeRegressaoApi(type: Test) {
    systemProperty 'cucumber.filter.tags', "not @wip and not @quarentena and not @sanidade"
}

dockerCompose {
    isRequiredBy(test)
    isRequiredBy(testeSanidadeApi)
    isRequiredBy(testeRegressaoApi)
}
